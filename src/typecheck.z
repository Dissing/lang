
use super::intmap::*;
use super::interning::*;
use super::source_map::*;
use super::ast::*;
use super::error::*;

CommonTypes :: struct {
  _void: *Ty,
  _constvoid: *Ty,
  _variadic: *Ty,
  _bool: *Ty,
  _char: *Ty,
  _constchar: *Ty,
  _i8: *Ty,
  _i16: *Ty,
  _i32: *Ty,
  _i64: *Ty,
  _u8: *Ty,
  _u16: *Ty,
  _u32: *Ty,
  _u64: *Ty,
  _f32: *Ty,
  _f64: *Ty,
}

TypeContext :: struct {
  ast: *Ast,
  interner: *Interner,
  source_map: *SourceMap,
  common: CommonTypes,
  types_lookup: *IntMap,
  types: *Ty,
  next_type_idx: u32,
}

hash_ty :: (ty: *Ty) -> u64;

hash_fn :: (ty: *Ty) -> u64 {

  var def: FnDef = ty.node.function;
  var h: u64 = hash_ty(def.output);

  var i: u32 = 0;
  while i < def.num_parameters {
    h = h ^ (hash_ty(def.parameters[i]) + 2654435769 + (h << 6) + (h >> 2));
    i = i + 1;
  }
  return h;
}

hash_path :: (path: Path) -> u64 {
  var h: u64 = path.num_segments;
  var i: u32 = 0;
  while i < path.num_segments {
    h = h ^ (path.segments[i].name.x + 2654435769 + (h << 6) + (h >> 2));
    i = i + 1;
  }
  return h;
}

hash_ty :: (ty: *Ty) -> u64 {

  var h: u64;

  h = ty.kind;

  if ty.kind == TyKind::Signed || ty.kind == TyKind::Unsigned  h = h ^ (integer_size(ty.node.integer) << 4);
  else if ty.kind == TyKind::Floating  h = h ^ (floating_size(ty.node.floating) << 4);
  else if ty.kind == TyKind::Struct || ty.kind == TyKind::Union  h = h ^ (hash_path(ty.node.compound.path) << 4);
  else if ty.kind == TyKind::Enum  h = h ^ (hash_path(ty.node._enum.path) << 4);
  else if ty.kind == TyKind::Fn  h = h ^ (hash_fn(ty) << 4);
  else if ty.kind == TyKind::Ptr  h = h ^ (hash_ty(ty.node.ptr) << 4);

  return h;
}

intern_ty :: (ctx: *TypeContext, ty: Ty) -> *Ty {
  val h: u64 = hash_ty(&ty);
  var ty_idx: u32 = intmap_lookup(ctx.types_lookup, h);
  if ty_idx == 0 {
    ty_idx = ctx.next_type_idx;
    ctx.next_type_idx = ctx.next_type_idx + 1;
    intmap_insert(ctx.types_lookup, h, ty_idx);
    ctx.types[ty_idx] = ty;
  }
  return &ctx.types[ty_idx];
}

create_base_type :: (ctx: *TypeContext, kind: TyKind) -> *Ty {
  val ty: Ty;
  ty.kind = kind;
  return intern_ty(ctx, ty);
}

create_float_type :: (ctx: *TypeContext, size: FloatingSize) -> *Ty {
  val ty: Ty;
  ty.kind = TyKind::Floating;
  ty.node.floating = size;
  return intern_ty(ctx, ty);
}

create_int_type :: (ctx: *TypeContext, kind: TyKind, size: IntegerSize) -> *Ty {
  val ty: Ty;
  ty.kind = kind;
  ty.node.integer = size;
  return intern_ty(ctx, ty);
}

create_common_types :: (ctx: *TypeContext) {
  val t: *CommonTypes = &ctx.common;
  t._void = create_base_type(ctx, TyKind::Void);
  t._constvoid = create_base_type(ctx, TyKind::ConstVoid);
  t._variadic = create_base_type(ctx, TyKind::Variadic);
  t._bool = create_base_type(ctx, TyKind::Bool);
  t._char = create_base_type(ctx, TyKind::Char);
  t._constchar = create_base_type(ctx, TyKind::ConstChar);

  t._i8 = create_int_type(ctx, TyKind::Signed, IntegerSize::I8);
  t._i16 = create_int_type(ctx, TyKind::Signed, IntegerSize::I16);
  t._i32 = create_int_type(ctx, TyKind::Signed, IntegerSize::I32);
  t._i64 = create_int_type(ctx, TyKind::Signed, IntegerSize::I64);

  t._u8 = create_int_type(ctx, TyKind::Unsigned, IntegerSize::I8);
  t._u16 = create_int_type(ctx, TyKind::Unsigned, IntegerSize::I16);
  t._u32 = create_int_type(ctx, TyKind::Unsigned, IntegerSize::I32);
  t._u64 = create_int_type(ctx, TyKind::Unsigned, IntegerSize::I64);

  t._f32 = create_float_type(ctx, FloatingSize::F32);
  t._f64 = create_float_type(ctx, FloatingSize::F64);
}

ast_type_to_ty :: (ctx: *TypeContext, ast_ty: *AstType) -> *Ty;

primitive_to_ty :: (ctx: *TypeContext, prim: PrimitiveTy) -> *Ty {
  val t: *CommonTypes = &ctx.common;
  if prim.kind == PrimitiveTyKind::Void  return t._void;
  else if prim.kind == PrimitiveTyKind::Bool  return t._bool;
  else if prim.kind == PrimitiveTyKind::Char  return t._char;

  else if prim.kind == PrimitiveTyKind::ConstChar  return t._constchar;
  else if prim.kind == PrimitiveTyKind::ConstVoid  return t._constvoid;

  else if prim.kind == PrimitiveTyKind::Signed {
    if prim.node.integer == IntegerSize::I8  return t._i8;
    else if prim.node.integer == IntegerSize::I16  return t._i16;
    else if prim.node.integer == IntegerSize::I32  return t._i32;
    else if prim.node.integer == IntegerSize::I64  return t._i64;
    else  abort();
  }
  else if prim.kind == PrimitiveTyKind::Unsigned {
    if prim.node.integer == IntegerSize::I8  return t._u8;
    else if prim.node.integer == IntegerSize::I16  return t._u16;
    else if prim.node.integer == IntegerSize::I32  return t._u32;
    else if prim.node.integer == IntegerSize::I64  return t._u64;
    else  abort();
  }
  else if prim.kind == PrimitiveTyKind::Floating {
    if prim.node.floating == FloatingSize::F32  return t._f32;
    else if prim.node.floating == FloatingSize::F64  return t._f64;
    else  abort();
  }
  else  abort();
}

ast_path_to_ty :: (ctx: *TypeContext, ast_ty: *AstType) -> *Ty {
  val binding: Binding = ast_ty.node.path.binding;

  if binding.kind == BindingKind::PrimitiveType {
    return primitive_to_ty(ctx, binding.node.primitive);
  }
  else if binding.kind == BindingKind::Item {
    val item: *Item = binding.node.item;
    if item.kind == ItemKind::Struct || item.kind == ItemKind::Union  return item.node.compound.ty;
    else if item.kind == ItemKind::Function  return item.node.function.header.ty;
    else if item.kind == ItemKind::Enum  return item.node._enum.ty;
    else  return null;
  }
  else abort(); //TODO
}

ast_ptr_to_ty :: (ctx: *TypeContext, ast_ty: *AstType) -> *Ty {
  val ty: Ty;
  ty.kind = TyKind::Ptr;
  ty.node.ptr = ast_type_to_ty(ctx, ast_ty.node.ptr);
  return intern_ty(ctx, ty);
}

ast_type_to_ty :: (ctx: *TypeContext, ast_ty: *AstType) -> *Ty {
  if ast_ty.kind == AstTypeKind::Void  return ctx.common._void;
  else if ast_ty.kind == AstTypeKind::Path  return ast_path_to_ty(ctx, ast_ty);
  else if ast_ty.kind == AstTypeKind::Ptr  return ast_ptr_to_ty(ctx, ast_ty);
  else if ast_ty.kind == AstTypeKind::Variadic  return ctx.common._variadic;
  else abort();
}

ast_literal_to_ty :: (ctx: *TypeContext, lit: Literal) -> *Ty {
  if lit.kind == LiteralKind::Int  return ctx.common._i32;
  else if lit.kind == LiteralKind::Float  return ctx.common._f32;
  else if lit.kind == LiteralKind::Bool  return ctx.common._bool;
  else if lit.kind == LiteralKind::Char  return ctx.common._char;
  else if lit.kind == LiteralKind::Str {
    val str_ty: Ty;
    str_ty.kind = TyKind::Ptr;
    str_ty.node.ptr = ctx.common._char;
    return intern_ty(ctx, str_ty);
  }
  else  abort();
}

coerce_type :: (ctx: *TypeContext, from: *Ty, to: *Ty) -> u32 {

  if from == to  return 1;
  else if to.kind == TyKind::Variadic  return 1;
  else if from.kind == TyKind::Signed || from.kind == TyKind::Unsigned {
    val from_size: u32 = integer_size(from.node.integer);

    if to.kind == TyKind::Signed || to.kind == TyKind::Unsigned {
      val to_size: u32 = integer_size(to.node.integer);
      if from_size <= to_size  return 2;
    }
    if to.kind == TyKind::Bool  return 2;

    if to.kind == TyKind::Ptr  return 2;
  }
  else if from.kind == TyKind::Ptr {
    val inner_from: *Ty = from.node.ptr;

    if to.kind == TyKind::Bool  return 2;

    else if to.kind == TyKind::Ptr {
      val inner_to: *Ty = to.node.ptr;
      if inner_from.kind == TyKind::Void || inner_from.kind == TyKind::ConstVoid  return 2;
      if inner_to.kind == TyKind::Void || inner_to.kind == TyKind::ConstVoid  return 2;

      if inner_from.kind == TyKind::Char && inner_to.kind == TyKind::ConstChar  return 2;
    }
  }
  return 0;
}

coerce_binary_expr :: (ctx: *TypeContext, expr: *Expr, left: *Ty, right: *Ty) {

  val coerce_left: u32 = coerce_type(ctx, left, right);
  val coerce_right: u32 = coerce_type(ctx, right, left);

  if coerce_left == 1 || coerce_right == 1 {}
  else if coerce_left == 2 {
    val cast_expr: *Expr = ast_create_expr(ctx.ast, ExprKind::Cast);
    cast_expr.node._cast.ty = right;
    cast_expr.node._cast.inner = expr.node.binary.left;
    expr.node.binary.left = cast_expr;
  }
  else if coerce_right == 2 {
    val cast_expr: *Expr = ast_create_expr(ctx.ast, ExprKind::Cast);
    cast_expr.node._cast.ty = left;
    cast_expr.node._cast.inner = expr.node.binary.right;
    expr.node.binary.right = cast_expr;
  }
  else  emit_error(ctx.source_map, expr.span, "Cannot coerce type in binary expression");
}

coerce_expr_type :: (ctx: *TypeContext, expr: *Expr, from: *Ty, to: *Ty) -> *Expr {
  val can_coerce: u32 = coerce_type(ctx, from, to);
  if can_coerce == 1  return expr;
  else if can_coerce == 2 {
    val cast_expr: *Expr = ast_create_expr(ctx.ast, ExprKind::Cast);
    cast_expr.node._cast.ty = to;
    cast_expr.node._cast.inner = expr;
    return cast_expr;
  }
  else emit_error(ctx.source_map, expr.span, "Cannot coerce type");
  abort();
}

check_expr :: (ctx: *TypeContext, expr: *Expr) -> *Ty;
check_item :: (ctx: *TypeContext, item: *Item);
check_mod :: (ctx: *TypeContext, module: *Module);

check_unary :: (ctx: *TypeContext, expr: *Expr) -> *Ty {
  var inner_ty: *Ty = check_expr(ctx, expr.node.unary.inner);
  val op: UnaryOperatorKind = expr.node.unary.op;
  if op == UnaryOperatorKind::Deref {
    if inner_ty.kind == TyKind::Ptr {
      return inner_ty.node.ptr;
    }
    else  emit_error(ctx.source_map, expr.span, "Trying to deref non-pointer");
  }
  else if op == UnaryOperatorKind::Refer {
    val ptr_ty: Ty;
    ptr_ty.kind = TyKind::Ptr;
    ptr_ty.node.ptr = inner_ty;
    return intern_ty(ctx, ptr_ty);
  }
  else if op == UnaryOperatorKind::Negation {
    return inner_ty;
  }
  else if op == UnaryOperatorKind::Complement {
    expr.node.unary.inner = coerce_expr_type(ctx, expr.node.unary.inner, inner_ty, ctx.common._bool);
    return ctx.common._bool;
  }
  abort();
}

operator_is_boolean :: (op: BinaryOperatorKind) -> bool {
  return op == BinaryOperatorKind::Less || op == BinaryOperatorKind::LessEq || op == BinaryOperatorKind::NotEq ||
    op == BinaryOperatorKind::Greater || op == BinaryOperatorKind::GreaterEq || op == BinaryOperatorKind::Equality ||
    op == BinaryOperatorKind::And || op == BinaryOperatorKind::Or;
}

check_binary :: (ctx: *TypeContext, expr: *Expr) -> *Ty {
  val op: BinaryOperatorKind = expr.node.binary.op;
  var left: *Ty = check_expr(ctx, expr.node.binary.left);
  var right: *Ty = check_expr(ctx, expr.node.binary.right);

  coerce_binary_expr(ctx, expr, left, right);

  if operator_is_boolean(op)  return ctx.common._bool;
  else  return left;
}

check_block :: (ctx: *TypeContext, block: *Block) {
  var i: u32 = 0;
  while i < block.num_exprs {
    check_expr(ctx, block.exprs[i]);
    i = i + 1;
  }
}

check_block_expr :: (ctx: *TypeContext, expr: *Expr) -> *Ty {
  check_block(ctx, expr.node.block);
  return ctx.common._void;
}

check_call :: (ctx: *TypeContext, expr: *Expr) -> *Ty {
  var call: CallData = expr.node.call;
  val fn_ty: *Ty = check_expr(ctx, call.func);
  if fn_ty.kind != TyKind::Fn  emit_warning(ctx.source_map, expr.span, "Tried to call non-function");

  val fn_def: *FnDef = &fn_ty.node.function;

  var is_variadic: bool = false;
  if fn_def.num_parameters > 0 {
    val param_ty: *Ty = fn_def.parameters[fn_def.num_parameters - 1];
    is_variadic = param_ty.kind == TyKind::Variadic;
  }

  if call.num_args == fn_def.num_parameters || (call.num_args >= (fn_def.num_parameters-1) && is_variadic)  {
    var i: i32 = 0;
    while i < call.num_args {
        val arg_ty: *Ty = check_expr(ctx, call.args[i]);
        if i < fn_def.num_parameters {
          call.args[i] = coerce_expr_type(ctx, call.args[i], arg_ty, fn_def.parameters[i]);
        }
        i = i + 1;
    }
  }
  else  emit_error(ctx.source_map, expr.span, "Called function with incorrect number of arguments");
  return fn_def.output;
}

check_conditional :: (ctx: *TypeContext, expr: *Expr) -> *Ty {
  var data: *ConditionalData = &expr.node.conditional;

  val cond_ty: *Ty = check_expr(ctx, data.condition);
  data.condition = coerce_expr_type(ctx, data.condition, cond_ty, ctx.common._bool);

  check_expr(ctx, data.then);
  if data.otherwise {
     check_expr(ctx, data.otherwise);
  }
  return ctx.common._void;
}

check_while :: (ctx: *TypeContext, expr: *Expr) -> *Ty {
  var data: WhileData = expr.node.whl;
  var condition: *Expr = data.condition;
  val cond_ty: *Ty = check_expr(ctx, condition);
  if cond_ty.kind != TyKind::Bool  emit_warning(ctx.source_map, condition.span, "Condition must be of type bool");
  check_expr(ctx, data.body);
  return ctx.common._void;
}

check_indexing :: (ctx: *TypeContext, expr: *Expr) -> *Ty {
  val data: IndexingData = expr.node.indexing;
  val array_ty: *Ty = check_expr(ctx, data.array);
  if array_ty.kind != TyKind::Ptr   emit_error(ctx.source_map, expr.span, "Cannot index into non-ptr type");
  val index_ty: *Ty = check_expr(ctx, data.index);
  if !(index_ty.kind == TyKind::Signed || index_ty.kind == TyKind::Unsigned) {
     emit_error(ctx.source_map, expr.span, "Only integers are valid indices");
  }
  return array_ty.node.ptr;
}

check_field :: (ctx: *TypeContext, expr: *Expr) -> *Ty {
  val data: FieldData = expr.node.field;
  var compound_ty: *Ty = check_expr(ctx, data.strct);

  var ptr_inner: *Ty = compound_ty.node.ptr;
  if compound_ty.kind == TyKind::Ptr && (ptr_inner.kind == TyKind::Struct || ptr_inner.kind == TyKind::Union) {
    compound_ty = compound_ty.node.ptr;
  }

  if compound_ty.kind != TyKind::Struct && compound_ty.kind != TyKind::Union   emit_error(ctx.source_map, expr.span, "Cannot access field of non-compound type");

  val compound: CompoundDef = compound_ty.node.compound;

  var i: u32 = 0;
  while i < compound.num_fields {
    if compound.fields[i].name.x == data.ident.name.x {
      return compound.fields[i].ty;
    }
    i = i + 1;
  }
  emit_warning(ctx.source_map, expr.span, "Compound type has no such field");
  return null;
}

check_path :: (ctx: *TypeContext, path: *Path) -> *Ty {
  if path.binding.kind == BindingKind::Item {
    val item: *Item = path.binding.node.item;
    if item.kind == ItemKind::Struct || item.kind == ItemKind::Union {
      return item.node.compound.ty;
    }
    else if item.kind == ItemKind::Enum {
      return item.node._enum.ty;
    }
    else if item.kind == ItemKind::Function {
      return item.node.function.header.ty;
    }
    else if item.kind == ItemKind::Const || item.kind == ItemKind::Variable {
      return item.node.variable.ty;
    }
    else  abort();
  }
  else if path.binding.kind == BindingKind::Variable {
    val data: *LetData = path.binding.node.variable;
    return data.ty;
  }
  else if path.binding.kind == BindingKind::Parameter {
    val param: *FunctionParameter = path.binding.node.parameter;
    return param.ty;
  }
  else if path.binding.kind == BindingKind::Variant {
    val variant: *EnumVariant = path.binding.node.variant;
    val enum_item: *Item = variant._enum;
    return enum_item.node._enum.ty;
  }
  else if path.binding.kind == BindingKind::PrimitiveType {
    return primitive_to_ty(ctx, path.binding.node.primitive);
  }
  else abort();
}


check_let :: (ctx: *TypeContext, data: *LetData) -> *Ty {
  data.ty = ast_type_to_ty(ctx, data.ast_ty);
  if data.value  check_expr(ctx, data.value);
  return ctx.common._void;
}

check_assignment :: (ctx: *TypeContext, assignment: AssignmentData) -> *Ty {
  check_expr(ctx, assignment.left);
  check_expr(ctx, assignment.right);
  return ctx.common._void;
}

check_return :: (ctx: *TypeContext, expr: *Expr) -> *Ty {
  check_expr(ctx, expr);
  return ctx.common._void;
}

check_sizeof :: (ctx: *TypeContext, expr: *Expr) -> *Ty {
  check_expr(ctx, expr);
  return ctx.common._u64;
}

check_cast :: (ctx: *TypeContext, data: *CastData) -> *Ty {
  check_expr(ctx, data.inner);
  data.ty = ast_type_to_ty(ctx, data.ast_ty);
  return data.ty;
}

check_expr :: (ctx: *TypeContext, expr: *Expr) -> *Ty {
  val result: *Ty;
  if expr.kind == ExprKind::Unary  result = check_unary(ctx, expr);
  else if expr.kind == ExprKind::Binary  result = check_binary(ctx, expr);
  else if expr.kind == ExprKind::Block  result = check_block_expr(ctx, expr);
  else if expr.kind == ExprKind::Call  result = check_call(ctx, expr);
  else if expr.kind == ExprKind::Conditional  result = check_conditional(ctx, expr);
  else if expr.kind == ExprKind::ControlFlow  result = ctx.common._void;
  else if expr.kind == ExprKind::While  result = check_while(ctx, expr);
  else if expr.kind == ExprKind::Indexing  result = check_indexing(ctx, expr);
  else if expr.kind == ExprKind::Field  result = check_field(ctx, expr);
  else if expr.kind == ExprKind::Path  result = check_path(ctx, &expr.node.path);
  else if expr.kind == ExprKind::Literal  result = ast_literal_to_ty(ctx, expr.node.lit);
  else if expr.kind == ExprKind::Assignment  result = check_assignment(ctx, expr.node.assignment);
  else if expr.kind == ExprKind::Let  result = check_let(ctx, &expr.node._let);
  else if expr.kind == ExprKind::Return  result = check_return(ctx, expr.node._return);
  else if expr.kind == ExprKind::Sizeof  result = check_sizeof(ctx, expr.node._sizeof);
  else if expr.kind == ExprKind::Cast  result = check_cast(ctx, &expr.node._cast);
  else abort();
  expr.ty = result;
  return result;
}


check_variable :: (ctx: *TypeContext, item: *Item) {
  var data: *VariableData = &item.node.variable;
  var decl_ty: *Ty = data.ty;

  if item.node.variable.body {
    var expr_ty: *Ty = check_expr(ctx, data.body);

    data.body = coerce_expr_type(ctx, data.body, expr_ty, decl_ty);
  }
}

check_function :: (ctx: *TypeContext, item: *Item) {

  if item.node.function.body {
    check_expr(ctx, item.node.function.body);
  }
}

check_item :: (ctx: *TypeContext, item: *Item) {
  if item.kind == ItemKind::Enum  {}
  else if item.kind == ItemKind::Struct || item.kind == ItemKind::Union  {}
  else if item.kind == ItemKind::Use  {}
  else if item.kind == ItemKind::Module  check_mod(ctx, item.node.module);
  else if item.kind == ItemKind::Const || item.kind == ItemKind::Variable  check_variable(ctx, item);
  else if item.kind == ItemKind::Function  check_function(ctx, item);
  else abort();
}

check_mod :: (ctx: *TypeContext, module: *Module) {
  var i: u32 = 0;
  while i < module.num_items {
    check_item(ctx, module.items[i]);
    i = i + 1;
  }
}

collect_enum :: (ctx: *TypeContext, item: *Item, path: Path) {

  val ty: *Ty = item.node._enum.ty;
  val def: *EnumDef = &ty.node._enum;

  def.variants = malloc(sizeof(Sid) * item.node._enum.num_variants);
  var i: u32 = 0;
  while i < item.node._enum.num_variants {
    def.variants[i] = item.node._enum.variants[i].ident.name;
    i = i + 1;
  }
  def.num_variants = item.node._enum.num_variants;
}

collect_variable :: (ctx: *TypeContext, item: *Item) {
  var data: *VariableData = &item.node.variable;
  data.ty = ast_type_to_ty(ctx, data.ast_ty);
}

collect_function :: (ctx: *TypeContext, item: *Item) {

  val header: *FunctionHeader = &item.node.function.header;

  val def: FnDef;

  def.output = ast_type_to_ty(ctx, header.output_ast_ty);
  def.num_parameters = header.num_parameters;
  def.parameters = malloc(8 * def.num_parameters);

  var i: u32 = 0;
  while i < def.num_parameters {
    val param_ty: *Ty = ast_type_to_ty(ctx, header.parameters[i].ast_ty);
    def.parameters[i] = param_ty;
    header.parameters[i].ty = param_ty;
    i = i + 1;
  }

  val ty: Ty;
  ty.kind = TyKind::Fn;
  ty.node.function = def;

  item.node.function.header.ty = intern_ty(ctx, ty);
}

collect_compound :: (ctx: *TypeContext, item: *Item, path: Path) {
  val ty: *Ty = item.node.compound.ty;
  val def: *CompoundDef = &ty.node.compound;

  if item.node.compound.num_fields > 0 {

    def.fields = malloc(sizeof(FieldDef) * item.node.compound.num_fields);
    var i: u32 = 0;
    while i < item.node.compound.num_fields {
      def.fields[i].name = item.node.compound.fields[i].ident.name;
      def.fields[i].ty = ast_type_to_ty(ctx, item.node.compound.fields[i].ast_ty);
      i = i + 1;
    }
    def.num_fields = item.node.compound.num_fields;
  }

}

collect_mod :: (ctx: *TypeContext, module: *Module);

collect_item :: (ctx: *TypeContext, item: *Item, path: Path) {
  if item.kind == ItemKind::Enum  collect_enum(ctx, item, path);
  else if item.kind == ItemKind::Function  collect_function(ctx, item);
  else if item.kind == ItemKind::Variable || item.kind == ItemKind::Const  collect_variable(ctx, item);
  else if item.kind == ItemKind::Struct || item.kind == ItemKind::Union  collect_compound(ctx, item, path);
  else if item.kind == ItemKind::Module  collect_mod(ctx, item.node.module);
}

collect_mod :: (ctx: *TypeContext, module: *Module) {
  var i: u32 = 0;
  while i < module.num_items {
    var item: *Item = module.items[i];
    var idx: u32 = intmap_lookup(module.index_lookup, item.ident.name.x);
    collect_item(ctx, item, module.index[idx].path);
    i = i + 1;
  }
}

collect_names :: (ctx: *TypeContext, module: *Module) {
  var i: u32 = 0;
  while i < module.num_items {
    var item: *Item = module.items[i];
    if item.kind == ItemKind::Struct || item.kind == ItemKind::Union {
      val def: CompoundDef;
      def.path.num_segments = module.path.num_segments + 1;
      def.path.segments = malloc(sizeof(Ident) * def.path.num_segments);
      def.path.binding.kind = BindingKind::Item;
      def.path.binding.node.item = item;
      memcpy(def.path.segments, module.path.segments, sizeof(Ident) * module.path.num_segments);
      def.path.segments[module.path.num_segments] = item.ident;

      val ty: Ty;
      if item.kind == ItemKind::Struct  ty.kind = TyKind::Struct;
      else if item.kind == ItemKind::Union  ty.kind = TyKind::Union;

      ty.node.compound = def;

      item.node.compound.ty = intern_ty(ctx, ty);
    }
    else if item.kind == ItemKind::Enum {

      val def: EnumDef;
      def.path.num_segments = module.path.num_segments + 1;
      def.path.segments = malloc(sizeof(Ident) * def.path.num_segments);
      def.path.binding.kind = BindingKind::Item;
      def.path.binding.node.item = item;
      memcpy(def.path.segments, module.path.segments, sizeof(Ident) * module.path.num_segments);
      def.path.segments[module.path.num_segments] = item.ident;

      val ty: Ty;
      ty.kind = TyKind::Enum;

      ty.node._enum = def;

      item.node._enum.ty = intern_ty(ctx, ty);
    }
    else if item.kind == ItemKind::Module  collect_names(ctx, item.node.module);
    i = i + 1;
  }
}

check :: (sess: *Session, ast: *Ast) {
  var ctx: TypeContext;

  ctx.ast = ast;
  ctx.interner = &sess.interner;
  ctx.source_map = &sess.source;
  ctx.types_lookup = intmap_create(1024);
  ctx.types = malloc(sizeof(Ty) * 1024);
  ctx.next_type_idx = 1;
  create_common_types(&ctx);

  collect_names(&ctx, ast.root_module);
  collect_mod(&ctx, ast.root_module);
  check_mod(&ctx, ast.root_module);
}
